apply plugin: 'java'
apply plugin: "com.github.hierynomus.license"
apply plugin: 'nebula.maven-publish'
apply plugin: 'findbugs'
apply plugin: "jacoco"

sourceCompatibility = 1.8

configurations.all {
    // To prevent slf4j complaining about multiple logging implementations (and failing), trick gradle into providing only a single
    // implmentation (logback).
    resolutionStrategy.dependencySubstitution {
        // Substitute one module dependency for another
        substitute module('org.slf4j:slf4j-log4j12') with module('ch.qos.logback:logback-core:1.1.3')
        substitute module('org.slf4j:log4j-over-slf4j') with module('ch.qos.logback:logback-core:1.1.3')
    }
}

dependencies {
    compile(project(":api")){
        exclude group: 'org.slf4j'
        exclude group: 'javax.servlet'
    }
    compile 'io.dropwizard:dropwizard-core:1.0.2'
    testCompile 'io.dropwizard:dropwizard-testing:1.0.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}


publishing {
    repositories {
        maven {
            credentials {
                username project.hasProperty("kebernet_bintray") == null ? "" : project.getProperties().get("kebernet_bintray")
                password project.hasProperty("kebernet_bintray_api") == null ? "" :  project.getProperties().get("kebernet_bintray_api")
            }
            url "https://api.bintray.com/maven/kebernet/maven/skillz/"
        }
    }
    publications {
        nebula(MavenPublication) {
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                asNode().appendNode('url', 'https://github.com/kebernet/skillz')
                asNode().appendNode('inceptionYear', '2016')
                def lic = asNode().appendNode('licenses')
                        .appendNode('license')
                lic.appendNode('name', 'Apache Software License, Version 2.0')
                lic.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                lic.appendNode('distribution', 'repo')
                def scm = asNode().appendNode('scm')
                scm.appendNode('connection', 'scm:git:git@github.com:kebernet/skillz.git')
                scm.appendNode('url', 'https://github.com/kebernet/skillz')
            }
        }
    }
}

license {
    header rootProject.file('etc/header.txt')
    strictCheck true
    mapping("java", "SLASHSTAR_STYLE")
}

